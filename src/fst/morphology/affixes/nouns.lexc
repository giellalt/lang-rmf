!! # Noun inflection
!  ---------------
!! The inflection first gives N, Nom and gender tags, then point to oblique cases.

!! ## The noun continuation lexica

!! ### Masculines

LEXICON RAKLO
!! **LEXICON @LEXNAME@** adds -o for Sg Nom and redirects to MSCOBL
+N+Msc+Sg+Nom:%>o # ;
+N+Msc:%>e MSCOBL ;

LEXICON LAU
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:u # ;
+N+Msc:^Vv%>e MSCOBL ;

LEXICON BEREH
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom: # ;
+N+Msc:%>e MSCOBL ;

LEXICON VUUDAR
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom: # ;
+N+Msc:%>e MSCOBL_SG ;

+N+Msc+Pl+Nom:%>ja # ; ! vuudarja
+N+Msc+Pl+Obl:%>en # ; ! vuudaren
+N+Msc+Pl+Obl:j%>en # ; ! vuudarjen
+N+Msc+Pl:%>en OBLCASES_PL ;
+N+Msc+Pl:j%>en OBLCASES_PL ;
+N+Msc+Pl+Gen:%>engo # ;
+N+Msc+Pl+Gen:j%>engo # ;
+N+Msc+Pl+Gen:%>en GENCASES ;
+N+Msc+Pl+Gen:j%>en GENCASES ;


LEXICON GRAI
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:rai # ;
+N+Msc+Sg+Obl:es # ;
+N+Msc+Sg+Dat:res%>ke # ;
+N+Msc+Sg+Abl:res%>ta # ;
+N+Msc+Sg+Ins:res%>sa # ;
+N+Msc+Sg+Gen:r%>esko # ;
+N+Msc+Sg+Gen:r%>es GENCASES ;

+N+Msc+Pl+Nom:raija # ; ! 
+N+Msc+Pl+Obl:re%>en # ; ! džeenen
+N+Msc+Pl:r%>en OBLCASES_PL ;
+N+Msc+Pl+Gen:r%>en%>ko # ;
+N+Msc+Pl+Gen:r%>en GENCASES ;

LEXICON DZII
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:ii # ;
+N+Msc+Sg+Obl:es # ;
+N+Msc+Sg+Dat:es%>ke # ;
+N+Msc+Sg+Abl:es%>ta # ;
+N+Msc+Sg+Ins:es%>sa # ;
+N+Msc+Sg+Gen:esko # ;
+N+Msc+Sg+Gen:es GENCASES ;

+N+Msc+Pl+Nom:iija # ; !  no i shortening
+N+Msc+Pl+Obl:%>en # ; ! džeenen
+N+Msc+Pl:%>en OBLCASES_PL ;
+N+Msc+Pl+Gen:%>engo # ;
+N+Msc+Pl+Gen:%>en GENCASES ;


LEXICON IBA
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:%>a # ;
+N+Msc:%>o MSCOBL_SG_ins_s ;
+N+Msc: MSCOBL_PL_o ;
+N+Msc+Sg+Ins:ossa # ;
+N+Msc+Pl+Gen:%>ongo # ;

LEXICON BEN
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom: # ;
+N+Msc:%>e MSCOBL ;

LEXICON OS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:%>os # ;
+N+Msc:%>o MSCOBL_SG ;
+N+Msc+Pl+Nom:%>i # ; ! džeene
+N+Msc+Pl+Obl:%>en # ; ! džeenen
+N+Msc+Pl:%>en OBLCASES_PL ;
+N+Msc+Pl+Gen:%>engo # ;
+N+Msc+Pl+Gen:%>en GENCASES ;


LEXICON NIS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:n%>is # ;
+N+Msc:n%>i MSCOBL_SG ;
IS_PL ;
+N+Msc+Pl+Gen:%>jengo # ;

LEXICON RIS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:r%>is # ;
+N+Msc:r%>i MSCOBL_SG ;
IS_PL ;

LEXICON TIS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:t%>is # ;
+N+Msc:t%>i MSCOBL_SG ;
IS_PL ;

LEXICON SSIS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:ss%>is # ;
+N+Msc:ss%>i MSCOBL_SG ;
IS_PL ;

LEXICON IS_PL
!! **LEXICON @LEXNAME@** 
+N+Msc+Pl+Nom:ja # ; ! džeene
+N+Msc+Pl+Obl:j%>en # ; ! džeenen
+N+Msc+Pl:j%>en OBLCASES_PL ;
+N+Msc+Pl+Gen:j%>engo # ;
+N+Msc+Pl+Gen:j%>en GENCASES ;


LEXICON ES
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:%>es # ;
+N+Msc:%>e MSCOBL_SG ;
+N+Msc+Pl+Nom:ja # ;
+N+Msc:%>e MSCOBL_PL_OBL ;


LEXICON US
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:%>us # ;
+N+Msc:%>u MSCOBL_SG ;
+N+Msc+Pl+Nom:u%>ja # ;
+N+Msc+Pl+Gen:u%>jengo # ;
+N+Msc:%>uje MSCOBL_PL_OBL ;

LEXICON YS
!! **LEXICON @LEXNAME@** 
+N+Msc+Sg+Nom:%>ys # ;
+N+Msc:%>y MSCOBL_SG ;
+N+Msc+Pl+Nom:y%>ja # ;
+N+Msc:%>yje MSCOBL_PL_OBL ;



!! ### Feminines

LEXICON BAAR
!! **LEXICON @LEXNAME@** pheen, tunnuksettomat fem
+N+Fem+Sg+Nom: # ; ! stem = nom
+N+Fem: FEMOBL ;   ! same forms in whole para

LEXICON KAALI
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>i # ;
KAALIOBL ;


LEXICON KAALIOBL
+N+Fem+Sg+Obl:%>ja # ; ! phenja
+N+Fem+Sg:%>ja OBLCASES_SG ;
+N+Fem+Sg+Gen:%>jako # ;        ! kaljako
+N+Fem+Sg+Gen+Kiiro:%>jakiiro # ;        ! kaljako
+N+Fem+Sg+Gen:%>ija^KK GENCASES ;  ! kaalijakkiiro
+N+Fem+Pl+Gen:%>jengo # ;        ! kaljako
+N+Fem+Pl+Gen:%>jen GENCASES ;  ! kaalijakkiiro

+N+Fem: FEMOBL_PL ; ! regular   ! kaljengiiro



LEXICON ANGRUSTI
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:i # ;
+N+Fem:i FEMOBL ; ! todo

LEXICON KHURMIN
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>n # ;
+N+Fem: FEMOBL ; ! todo

LEXICON BORNIK
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>k # ;
+N+Fem: FEMOBL ; ! todo

LEXICON DAI
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>i # ;
+N+Fem: DAIOBL ; ! todo

LEXICON TSIMB
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom: # ; ! removed %>k
+N+Fem: FEMOBL ; ! todo



LEXICON SKOOLA
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>a # ;
+N+Fem: FEMOBL_a ; ! todo

LEXICON RAADZA
!! **LEXICON @LEXNAME@** 
+N+Fem+Sg+Nom:%>a # ;
+N+Fem: FEMOBL ; ! todo



!! ## Morphology

!! ### Masculine morphology

LEXICON MSCOBL
!! **LEXICON @LEXNAME@** pointing to sg and pl lexica
MSCOBL_SG ;
MSCOBL_PL ;


LEXICON MSCOBL_SG 
!! **LEXICON @LEXNAME@** 
+Sg+Obl:s # ;
+Sg:s OBLCASES_SG ;
+Sg+Gen:sko # ;
+Sg+Gen:s GENCASES ;

LEXICON MSCOBL_SG_ins_s
!! **LEXICON @LEXNAME@** 
+Sg+Obl:s # ;
+Sg:s OBLCASES_SG_ins_s ;
+Sg+Gen:sko # ;
+Sg+Gen:s GENCASES ;


LEXICON MSCOBL_PL 
!! **LEXICON @LEXNAME@** 
+Pl+Nom: # ; ! džeene
+Pl+Obl:%>n # ; ! džeenen
+Pl:%>n OBLCASES_PL ;
+Pl+Gen:%>ngo # ;
+Pl+Gen:%>n GENCASES ;

LEXICON MSCOBL_PL_OBL 
!! **LEXICON @LEXNAME@** 
+Pl+Obl:%>n # ; ! džeenen
+Pl:%>n OBLCASES_PL ;
+Pl+Gen:%>ngo # ;
+Pl+Gen:%>n GENCASES ;

LEXICON MSCOBL_PL_o 
!! **LEXICON @LEXNAME@** 
+Pl+Nom:%>i # ; ! džeene
+Pl+Obl:%>on # ; ! džeenen
+Pl:%>on OBLCASES_PL ;
+Pl+Gen:%>on GENCASES ;



!! ### Feminine morphology


LEXICON FEMOBL
!! **LEXICON @LEXNAME@** pointing to sg and pl lexica
FEMOBL_SG ;
FEMOBL_PL ;


LEXICON FEMOBL_SG 
!! **LEXICON @LEXNAME@** 
+Sg+Obl:%>ja # ; ! phenja
!+Sg+Obl:%>a # ; ! xxx check this tšimba
!+Sg+Obl:%>e # ; ! xxx check this tšimbe
+Sg:%>ja OBLCASES_SG ;
!+Sg:%>a OBLCASES_SG ;
!+Sg:%>e OBLCASES_SG ;
+Sg+Gen:%>jako # ;
+Sg+Gen:%>ja GENCASES ;

LEXICON FEMOBL_PL 
!! **LEXICON @LEXNAME@** 
+Pl+Nom:%>ja # ; ! kalja
+Pl+Obl:%>jen # ; ! kaljen
+Pl:%>jen OBLCASES_PL ;
+Pl+Gen:%>jengo # ;
+Pl+Gen:%>jen GENCASES ;

LEXICON FEMOBL_a
!! **LEXICON @LEXNAME@** pointing to sg and pl lexica
FEMOBL_SG_a ;
FEMOBL_PL_a ;

LEXICON FEMOBL_SG_a 
!! **LEXICON @LEXNAME@** 
+Sg+Obl:%>a # ; ! skoola
+Sg:%>a OBLCASES_SG ;
+Sg+Gen:%>ako # ;
+Sg+Gen:%>a GENCASES ;

LEXICON FEMOBL_PL_a 
!! **LEXICON @LEXNAME@** 
+Pl+Nom:%>i # ; ! skooli
+Pl+Obl:%>en # ; ! kaljen
+Pl:%>en OBLCASES_PL ;
+Pl+Gen:%>engo # ;
+Pl+Gen:%>en GENCASES ;

LEXICON FEMOBL_PL_OBL 
!! **LEXICON @LEXNAME@** 
+Pl+Obl:%>en # ; ! džeenen
+Pl:%>en OBLCASES_PL ;
+Pl+Gen:%>en GENCASES ;

LEXICON FEMOBL_PL_o 
!! **LEXICON @LEXNAME@** 
+Pl+Nom:%>i # ; ! džeene
+Pl+Obl:%>on # ; ! džeenen
+Pl:%>on OBLCASES_PL ;
+Pl+Gen:%>on GENCASES ;


!! ### The oblique cases *-ke, -ta, -ha*

LEXICON OBLCASES_SG
!! **LEXICON @LEXNAME@** 
+Dat:%>ke # ;
+Abl:%>ta # ;
+Ins:^CDEL%>ha # ; ! -ha without s

LEXICON OBLCASES_SG_ins_s
!! **LEXICON @LEXNAME@** 
+Dat:%>ke # ;
+Abl:%>ta # ;
+Ins:%>ha # ; ! -sha with s

LEXICON OBLCASES_PL
!! **LEXICON @LEXNAME@** 
+Dat:%>ge # ;
+Abl:%>na # ;
+Ins:%>sa # ;

LEXICON GENCASES
!! **LEXICON @LEXNAME@** 
+Kiiro:%>kiiro # ; ! fix tags
+Kiiro:%>kiiri # ;
+Kiiro:%>kiire # ;


LEXICON DAIOBL
+Sg+Obl: # ;
+Sg+Dat:%>ke # ;
+Sg+Abl:%>tta # ;
+Sg+Ins:%>ha # ;
+Sg+Gen: GENCASES  ;
+Pl+Nom:i%>ja # ;
+Pl+Obl:i%>jen # ;
+Pl:%>jen OBLCASES_PL ;
! vim: set ft=xfst-lexc:

